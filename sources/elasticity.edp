/* Resolution of the linearized elasticity equation on the input shape */
include "./sources/inout.idp"
include "./sources/macros.idp"
load "medit"

/* Get mesh and sol names, and global parameters */
string MESH       = getsParam(EXCHFILE,"MeshName");
string SOLPATTERN      = getsParam(EXCHFILE,"DisplName");
string SOLEXTENSION    = getsParam(EXCHFILE,"DisplExtension");
int REFINT        = getiParam(EXCHFILE,"Refint");
int REFDIR        = getiParam(EXCHFILE,"Dirichlet");
int REFNEU        = getiParam(EXCHFILE,"Neumann");
int REFISO        = getiParam(EXCHFILE,"ReferenceBnd");
real loadx 	  = getrParam(EXCHFILE,"Loadx");
real loady 	  = getrParam(EXCHFILE,"Loady");

/* Loading mesh */
mesh Th = readmesh(MESH);

/* Finite element spaces and functions */
fespace Vh(Th,P1);
fespace Vh0(Th,P0);
Vh ux,uy, T;
Vh0 reg = region;


/* Mesh of the interior part and corresponding FE space */
mesh Thi = trunc(Th,(reg(x,y) == REFINT),label=REFINT);
fespace Vhi(Thi,P1);
Vhi uxi,uyi,vxi,vyi, Ti;



/* Variational formulation of the problem */
problem elas([uxi,uyi],[vxi,vyi]) = int2d(Thi)(sigeps(uxi,uyi,vxi,vyi))
                                   - int1d(Thi,REFNEU)(loadx*vxi+loady*vyi)
                                   + on(REFDIR,uxi=0.0,uyi=0.0);


elas;
	
printvec2(combinestrings(SOLPATTERN, 100, SOLEXTENSION),uxi[],uyi[]);



